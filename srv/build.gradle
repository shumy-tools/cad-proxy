plugins {
  id 'java'
  id 'maven'
  id 'org.xtext.xtend' version '2.0.2' // https://xtext.github.io/xtext-gradle-plugin/xtend.html
}

group 'pt.ieeta'
version '0.1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'jar'

repositories {
  mavenLocal()
  mavenCentral()
}

sourceSets {
  main.java.srcDirs= ['src']
}

dependencies {
  compile 'org.eclipse.xtend:org.eclipse.xtend.lib:2.15.0'
  compile 'ch.qos.logback:logback-classic:1.2.+' // 1.2.3
  
  compile 'info.picocli:picocli:3.+' // 3.7.0
  compile 'org.bouncycastle:bcprov-jdk15on:1.6'
  
  compile 'dcm4che:dcm4che-imageio:2.+' // 2.0.29
  compile 'dcm4che:dcm4che-net:2.+' // 2.0.29
  compile 'org.neo4j:neo4j:3.4.+' // 3.4.9
  
  compile 'com.google.code.gson:gson:2.+' // 2.8.5
  
  compile ('com.sparkjava:spark-core:2.+') { // 2.8.0
    exclude group: 'org.slf4j', module: 'slf4j-simple'
  }
}

jar {
  manifest {
    attributes 'Implementation-Title': project.name,
      'Implementation-Version': version, 
      'Built-By': 'micaelpedrosa@gmail.com',
      'Main-Class': 'CadProxyCLI',
      'Class-Path': configurations.runtime.resolvedConfiguration.resolvedArtifacts.collect {
        def gid = it.id.componentIdentifier.group
        "deps/$gid/" + it.file.name
      }.join(' ')
  }
}

task deps(type: Copy) {
  configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { dep ->
    def gid = dep.id.componentIdentifier.group
    copy {
      from dep.file
      into "$buildDir/libs/deps/$gid"
    }
  }
}